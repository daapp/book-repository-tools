#! /bin/sh
# \
exec tclsh "$0" ${1+"$@"}

package require fileutil

# path to unzip tool
set unzip unzip
set zip zip

proc head {filename} {
    fileutil::foreachLine l $filename {
        return $l
    }
}

proc convertFile {filename} {
    puts "1251 .. converting"
    set data [fileutil::cat -encoding cp1251 $filename]
    set data [regsub -nocase {encoding=['\"]windows-1251['\"]} $data \
        {encoding="utf-8"}]
    fileutil::writeFile -encoding utf-8 $filename $data
}

if {[string tolower [file extension [lindex $argv 0]]] eq ".fb2"} {
    convertFile [lindex $argv 0]
} else {
    set dir [fileutil::maketempdir]
    puts "Extract to $dir"

    set fb2zipname [lindex $argv 0]
    set fb2dirname [file dirname $fb2zipname]

    if {$fb2dirname eq "."} {
        set fb2dirname [pwd]
    }

    puts [exec $unzip -q -d $dir $fb2zipname]
    cd $dir
    foreach filename [glob -nocomplain *.fb2] {
        puts $filename

        if {[regexp -nocase {encoding=['\"]windows-1251['\"]} [head $filename]]} {
            convertFile $filename
            puts [exec $zip $filename.zip $filename]
            file rename $filename.zip $fb2dirname
        } else {
            puts "UTF ... skip"
        }
    }

    puts "Remove directory $dir"
    file delete -force -- $dir
}
